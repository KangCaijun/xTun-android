apply plugin: 'com.android.application'

if(file('../sign.gradle').exists()) {
    apply from: '../sign.gradle'
} else {
    logger.error('no such file: ../sign.gradle')
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'
    defaultConfig {
        applicationId "io.github.xTun"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 110
        versionName '1.1.1'
        jackOptions {
            enabled true
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        abortOnError true
    }
    buildTypes {
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
                logger.error('signing error: ' + ex)
            }
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                appendVersionNameVersionCode(variant, output, defaultConfig)
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }
    productFlavors {
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

def appendVersionNameVersionCode(variant, output, defaultConfig) {
    def versionName = defaultConfig.versionName
    if(variant.buildType.name == android.buildTypes.debug.name) {
        versionName += "-beta"
    }
    def file = output.outputFile
    output.outputFile = new File(file.parent, "xTun.apk".replace(".apk", "-v" + versionName + ".apk"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:support-annotations:25.0.1'
    compile "com.android.support:appcompat-v7:25.0.1"
    compile 'io.reactivex.rxjava2:rxjava:2.0.2'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.github.akarnokd:rxjava2-extensions:0.10.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    compile('com.mikepenz:materialdrawer:5.8.0@aar') {
        transitive = true
    }
    compile "com.mikepenz:iconics-core:2.8.1@aar"
    compile 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
}
